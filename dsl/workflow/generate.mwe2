module tools.vitruv.emftemplate.dsl

import org.eclipse.emf.mwe.utils.StandaloneSetup

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var workspaceRoot = ".."

Workflow {

    bean = StandaloneSetup {
        scanClassPath = true
        platformUri = workspaceRoot
    }

    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = "."
                rootPath = workspaceRoot

                runtime = {
                    root = workspaceRoot
                    src = "${workspaceRoot}/src/main/java"
                    srcGen = "${workspaceRoot}/target/generated-sources/xtext-java"
                }
            }
            code = {
                preferXtendStubs = false
                encoding = "UTF-8"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
            }
        }
        language = StandardLanguage {
            name = "tools.vitruv.emftemplate.dsl.ExampleLanguage"
            fileExtensions = "example"
            grammarUri = "platform:/resource/tools.vitruv.emftemplate.dsl/src/main/xtext/tools/vitruv/emftemplate/dsl/ExampleLanguage.xtext"
            
            referencedResource = "platform:/resource/tools.vitruv.emftemplate.model/src/main/ecore/model.genmodel"

            serializer = {
                generateStub = false
            }
            generator = {
                generateStub = true
            }
            scopeProvider = {
                generateStub = false
            }
            validator = {
                generateStub = true
            }
        }
    }

}
